apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext {
    geckoviewChannel = "stable"
    geckoviewVersion = "74.0.20200305014626"
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.lobobrowser"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 100000
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        alpha {
            dimension "version"
            versionNameSuffix "-alpha" + "-build" + getDate()
        }
        beta {
            dimension "version"
            versionNameSuffix "-beta" + "-build" + getDate()
        }
        stable {
            dimension "version"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // test dependencies
    testImplementation 'junit:junit:4.12'

    // support libraries
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.fragment:fragment:1.2.2'
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'

    // html parsing for reading mode
    implementation 'org.jsoup:jsoup:1.11.3'

    // dependency injection
    final def daggerVersion = '2.24'
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0-alpha02'

    // geko view
    implementation "org.mozilla.geckoview:geckoview:${geckoviewVersion}"
}

kapt {}

def getDate() {
    return new Date().format('yyyyMMddHHmmss')
}